version = '0.0.1'
group = 'net.rauros.jzwave'
ext.description = "Java Z-Wave"
ext.author = 'Chris Cavey'
ext.email = 'chris-jzwave@rauros.net'
ext.startYear = '2012'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'osgi'
apply plugin: 'eclipse'

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.5.0'
	}
}

apply plugin: 'license'
license {
    header rootProject.file('LICENSE.header')
	ext.year = project.ext.startYear + '-' + Calendar.getInstance().get(Calendar.YEAR)
    ext.name = project.ext.author
    ext.email = project.ext.email
	ext.product = project.name
}

task openzwaveConfigExport(type: net.rauros.groovy.ExportConfig)
compileJava.dependsOn(openzwaveConfigExport)

repositories {
	flatDir {
		dirs 'lib'
	}
	mavenCentral()
}

dependencies {
	compile group: 'com.neuronrobotics', name: 'nrjavaserial', version: '3.8.+'
	compile group: 'com.google.guava', name: 'guava', version: '+'
	compile group: 'org.slf4j', name: 'slf4j-api', version: '+'
	compile group: 'joda-time', name: 'joda-time', version: '+'
	compile group: 'ch.qos.logback', name: 'logback-core', version: '+'
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '+'
	compile group: 'ch.qos.logback', name: 'logback-access', version: '+'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

jar {
	manifest { // the manifest of the default jar is of type OsgiManifest
		instruction '-nouses', 'true'
		instruction 'Bundle-Symbolic', project.group
		instruction 'Bundle-Vendor', project.name
		instruction 'Bundle-Description', project.ext.description
		instruction 'Import-Package', '!javax.xml.*', "*"
	}
}

task(zwaveListen, dependsOn: 'classes', type: JavaExec) {
	group = 'zwave'
	description = 'Listen on Z-Wave interface and write output to console'
	main = 'net.rauros.jzwave.junit.TestEnumerate'
	classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
	args project.hasProperty('transport') ? project.transport : 'x'
}
